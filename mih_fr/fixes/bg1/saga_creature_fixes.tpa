DEFINE_ACTION_FUNCTION saga_creature_fixes
BEGIN
  // ## FIXED IN EEFP ##
  // Ankheg has wrong HD count (should be 8)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "%tutu_var%ankheg %tutu_var%ankhegg %tutu_var%ankhegq"
    editstring	= "level1=>8"
  END


  // Gnolls do not have enough strengh to wield their halberds
  // and in BGT/Tutu some are not labeled chaotic evil.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "flind1"
    editstring	= %patch_effect_inline=>~match=>"opcode = 54 and parameter1 = 4" parameter1=>-4~%	// %-signs are string delimiters here!
  END


  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "%tutu_var%gnoll %tutu_var%gnoll_a %tutu_var%gnoll_b %tutu_var%gnoll_c %tutu_var%gnoll_d %tutu_var%gnoll_e %tutu_var%gnoll02 %tutu_var%gnoll03 %tutu_var%gnoll3 %tutu_var%gnoll5 %tutu_var%gnoll5a %tutu_var%gnolld %tutu_scriptg%nollper %tutu_var%gnollsu "
    editstring	= ~strengthGT=>13 alignment=>"if gender = summoned then neutral else chaotic_evil"~
  END


  // wraith spiders are not marked as undead

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "%tutu_var%spidwr"
    editstring	= "general=>undead"	// TODO: Check cold resistance
  END


  // Some skeletons do not have their correct damage resistances.
  // (BGT/Tutu only, confirmed already fixed in EE.)

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "%tutu_var%iskelet %tutu_scripts%kelet02 %tutu_scripts%kelet03 %tutu_var%skeletb %tutu_var%skelets %tutu_scripts%kelpetr"
    editstring	= "resist_slashing=>50 resist_piercing=>50 resist_missile=>90"
  END


  // xvart2 and xvarth have wrong HD count for their xp value

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "%tutu_var%xvart2 %tutu_var%xvarth"
    editstring	= ~level1=>2 enforce_thac0=>at_worst enforce_saves=>at_worst enforce_hp=>at_worst~
  END
END	// saga_creature_fixes


