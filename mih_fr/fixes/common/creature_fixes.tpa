DEFINE_ACTION_FUNCTION creature_fixes
BEGIN
  // Summoned monsters should not leave corpses or items, or give XP.

  ACTION_DEFINE_ASSOCIATIVE_ARRAY summon_fix
  BEGIN
    xp_value=>0
    gold=>0
    no_corpse=>1
    patch_item_inline=>~undroppable=>1~
  END

  // Apply giant humanoid racial penalty to hit
  // dwarves/gnome/halflings consistently.

  ACTION_DEFINE_ASSOCIATIVE_ARRAY giant_fix
  BEGIN
    delete_effect=>"resource is_in [giant1 giant2 giant3]"
    add_effect_inline=>~number_to_add=>3 opcode=>177 target=>1 timing=>9 parameter2=>2 resource=>"entry_index from [giant1 giant2 giant3]"~
  END

  // Apply the above fixes

  COPY_EXISTING_REGEXP "^.*\.cre$" "override"
    LAUNCH_PATCH_FUNCTION FJ_CRE_VALIDITY
      INT_VAR
      do_message	= 1
      RET valid
    END

    PATCH_IF valid
    BEGIN
      LAUNCH_PATCH_FUNCTION CRE_read_gender
        RET
        gender	= value
      END

      PATCH_MATCH "%gender%"
      WITH
        "summoned"
        "summoned_demon"
        BEGIN
          PATCH_PRINT "%SOURCE_RES% is a summoned creature, applying fixes."

          LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext	= "CRE"
	    edits 	= "summon_fix"
          END
        END
        DEFAULT
      END

      LAUNCH_PATCH_FUNCTION CRE_read_general
        RET
        general	= value
      END

      PATCH_MATCH "%general%"
      WITH
        "gianthumanoid"
        BEGIN
          PATCH_PRINT "%SOURCE_RES% is a giant humanoid, applying fixes."

          LAUNCH_PATCH_FUNCTION apply_patches
	    STR_VAR
	    file_ext	= "CRE"
	    edits 	= "giant_fix"
	  END
        END
        DEFAULT
      END
    END

    BUT_ONLY_IF_IT_CHANGES


  ACTION_CLEAR_ARRAY summon_fix
  ACTION_CLEAR_ARRAY giant_fix


  // Some greater earth elementals have wrong HD number

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elearg01"
    editstring	= "levelGT=>16"
  END


  // Elemental paw enchantments are a bit all over the place.
  // We try to systemetically fix it here.
  // lesser elementals => +2
  // normal and greater => +3
  // princes (in tob) => +4

  // BGEE lacks elair.itm and elearl.itm for some reason, so we create them
  // if needed so the whole batch can be patched in one single place.

  ACTION_IF NOT FILE_EXISTS_IN_GAME "elearl.itm"
  BEGIN
    COPY_EXISTING "elear.itm" "elearl.itm"
  END

  ACTION_IF NOT FILE_EXISTS_IN_GAME "elair.itm"
  BEGIN
    COPY_EXISTING "elairl.itm" "elair.itm"
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "elair elairl elear elearl fireelel fireelem"
    editstring	= ~enchantment=>"object_index from [3 2 3 2 2 3]"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elairsu1 elairsuw sumelair"
    editstring	= ~replace_items=>"elairl(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elairsu2 elairsu3 elairsu4 swaair01 swaair02"
    editstring	= ~replace_items=>"elair(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elearsuw sumelear"
    editstring	= ~replace_items=>"elearl(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elearpr elearpr2 elearpr3 elearsu2 elearsu3 elearsu4 shearth swaear01 swaear02"
    editstring	= ~replace_items=>"elear(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elfirsu1 elfirsuw"
    editstring	= ~replace_items=>"fireelel(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "shfire"
    editstring	= ~replace_items=>"fireelem(weapon1)"~
  END


  // ## FIXED IN EEFP ##
  // Poison Mist can be both summoned and randomly encountered,
  // so we need both gender=>summoned and genter=>neither versions.

  ACTION_IF NOT FILE_EXISTS_IN_GAME "mistposu.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "mistpo01=>mistposu"
      editstring	= "xp_value=>0 gender=>summoned"
    END

    LAUNCH_ACTION_FUNCTION edit_creature
      STR_VAR
      creature		= "mistpo01"
      editstring	= "xp_value=>3500 gender=>neither"
    END

    LAUNCH_ACTION_FUNCTION edit_effect
      STR_VAR
      effect		= "sppois"
      editstring	= "resource=>mistposu"
    END
  END


  // Some planetars regenerate at an insane rate.  Tone it down a notch.

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "planevil plangood planwish"
    editstring	= %patch_effect_inline=>~match=>"opcode = 98" parameter1=>1~%
  END


  // Correct wraith spider bite to the actual P&P effects:
  // - 1d4 cold damage
  // - drains one level
  // - damages constitution
  // Effects are permanent until cured with Cure Disease and Restoration.

  MAKE_PATCH
    ability_dicenum=>0
    ability_dicesize=>0
    delete_effect=>null
    add_effect_inline'12=>~opcode=>12 target=>2 timing=>1 parameter2=>0x00020000 dicenum=>1 dicesize=>4~
    add_effect_inline'78=>~opcode=>78 target=>2 timing=>1 parameter1=>3 parameter2=>6~
    add_effect_inline'139=>~number_to_add=>2 opcode=>139 target=>2 timing=>1 parameter1=>"if enhanced_edition and not is_bg2 then entry_index from [25802 26453] else entry_index from [39752 41495]"~
    add_effect_inline'142=>~opcode=>142 target=>2 timing=>1 parameter2=>7~
    add_effect_inline'216=>~opcode=>216 target=>2 timing=>1 parameter1=>1~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "spidwr1"
    edits	= "patch_data"
  END
END	// creature_fixes


