DEFINE_ACTION_FUNCTION item_fixes
BEGIN
  // POTN17 - Exixir of Health
  //
  // Elixir of Health does not cure blindness or deafness, but it should.

  ACTION_IF FILE_EXISTS_IN_GAME "#curebld.spl"
  BEGIN
    MAKE_PATCH
      litvar_cure_spells=>"[#curebld #curedef]"
      clone_effect_inline=>~match=>"opcode = 146" number_to_add=>"length cure_spells" resource=>"entry_index from cure_spells"~
    END
  END
  ELSE
  BEGIN
    MAKE_PATCH
      litvar_cures_list=>"[75 79 81]"
      litvar_icons_list=>"[2 6 8 48 112]"
      clone_effect_inline'1=>~match=>"opcode = 11" number_to_add=>"length cures_list" opcode=>"entry_index from cures_list"~
      clone_effect_inline'2=>~match=>"opcode = 240" number_to_add=>"length icons_list" parameter2=>"entry_index from icons_list"~
    END
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "potn17"
    edits	= "patch_data"
  END


  // POTN20 - Antidote
  // POTN47 - Marek's Poison Antidote
  //
  // Contrary to what Hull seems to think, antidotes do not actually cure
  // drunkeness.  Let's have mercy on him and fix that oversight.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "potn17"
    editstring	= %clone_effect_inline=>~match=>"opcode = 11" opcode=>164~%
  END


  // SPERXX - All spears
  //
  // Spears in BG games are all two-handed and as such should do
  // a base damage of 1d6+1, not 1d6.

  MAKE_PATCH
    description1_string=>16283
    patch_ability_inline=>~match=>"ability_type = 1" damage_bonus=>"enchantment + 1"~
  END

  COPY_EXISTING_REGEXP "^.*\.itm$" "override"
    LAUNCH_PATCH_FUNCTION ITM_read_category
      RET
      item_type		= value
    END

    LAUNCH_PATCH_FUNCTION ITM_read_proficiency_code
      RET
      proficiency_code	= value
    END

    PATCH_IF item_type == 0x1d AND proficiency_code = 0x62
    BEGIN
      LAUNCH_PATCH_FUNCTION apply_patches
        STR_VAR
        file_ext	= "ITM"
        edits		= "patch_data"
      END
      
      LAUNCH_PATCH_FUNCTION ITM_read_description2_string
        RET
	desc_strref	= value
      END

      LAUNCH_PATCH_FUNCTION ITM_read_enchantment
        RET
	enchantment	= value
      END

      SET damage_bonus	= enchantment + 1

      PATCH_IF desc_strref >= 0 AND desc_strref < NEXT_STRREF
      BEGIN
        READ_STRREF IDENTIFIED_DESC old_desc

        INNER_PATCH_SAVE new_desc ~%old_desc%~
	BEGIN
	  REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
	   ": \(1 ?[dD] ?6\+ ?\)%enchantment%" ": \1%damage_bonus%"
	END
	
	INNER_ACTION
	BEGIN
	  STRING_SET_EVALUATE desc_strref ~%new_desc%~
	END
      END
    END

    BUT_ONLY_IF_IT_CHANGES


  // WAND03 - Wand of Magic Missiles
  //
  // A wand of Magic Missiles should launch two missiles, as per P&P.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "wand02"
    editstring	= ~patch_ability_inline=>"projectile=>69"~
  END
END	// item_fixes


