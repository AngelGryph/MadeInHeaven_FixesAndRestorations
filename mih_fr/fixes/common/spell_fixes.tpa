DEFINE_ACTION_FUNCTION spell_fixes
BEGIN
  // SPPR103 - Cure Light Wounds
  // SPPR315 - Cure Medium Wounds
  // SPPR401 - Cure Serious Wounds
  // SPPR502 - Cure Critical Wounds
  // SPPR514 - Mass Cure
  //
  // SPIN101 - Cure Light Wounds (Bhaalspawn innate)
  //
  // SPIN958 - Cure Serious Wounds (ulitharid innate)
  //
  // These all have the undocumented effect of during drunkenness.
  // This makes no real sense whatsoever.
  // NB: Paladin Lay on Hands already doesn't do this.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "sppr103 sppr315 sppr401 sppr502 sppr514 spin101 spin958"
    editstring	= ~delete_effect=>"opcode = 164"~
  END


  // SPPR108 - Remove Fear
  // SPCL222 - Remove Fear (Cavalier innate)
  //
  // Unlike the wizardly Resist Fear, these have the NON-COMBAT secondary
  // type instead of SPECIFICPROTECTIONS, resulting in inconsistent interaction
  // with spells like Breach.  This is likely an oversight, there are hints
  // these spells originally would not have a duration.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "sppr108 spcl222"
    editstring	= "secondary=>2"
  END


  // SPPR404 - Neutralize Poison
  //
  // Slow Poison cures drunkenness, but Neutralize Poison does not.  Let's
  // fix this oversight, shall we?

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "sppr404"
    editstring	= %clone_effect_inline=>~match=>"opcode = 11" opcode=>164~%
  END


  // SPPR707 - Sunray
  //
  // Spell does base 1000 damage to undead instead of using a slay effect
  // like the Mace of Disruption does, this looks kinda silly in the log
  // and bypasses Slay immunity, providing a loophole for an easy demi-lich
  // kill.  Change it to work like the Mace of Disruption.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "sppr707"
    editstring	= %patch_effect_inline=>~match=>"resource = unddeath" resource=>"die"~ clone_effect_inline=>~match=>"resource = die" resource=>"mesdie"~%
  END


  // SPWI213 - Stinking Cloud
  // SPWI004 - Stinking Cloud (Trap)
  // SPWM187 - Stinking Cloud (Wild Surge)
  // SPIN940 - Stinking Cloud (Mephit innate)
  //
  // Spell gives a +2 bonus on saves, but it should not.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spwi213 spwi004 spwm187 spin940"
    editstring	= %patch_effect_inline=>~match=>"save_vs_poison = 1" save_bonus=>0~%
  END


  // SPWI313 - Skull Trap
  //
  // This spell is supposed to do 2d4 + 1d4/level damage, and under
  // Mystra's Ban should cap at 12d4.

  MAKE_PATCH
    delete_ability=>~ability_min_level > 10~
    patch_effect_inline=>~match=>"opcode = 12" dicenum=>"dicenum + enhanced_edition + 1" dicesize=>4~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spwi313"
    edits	= "patch_data"
  END
END	// spell_fixes


