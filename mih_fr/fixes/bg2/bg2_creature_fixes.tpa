DEFINE_ACTION_FUNCTION bg2_creature_fixes
BEGIN
  // Ankheg HD fix

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "ankheg01"
    editstring	= "level1=>8"
  END


  // Gnoll strength and alignment fix

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "flind1"
    editstring	= %patch_effect_inline=>~match=>"opcode = 54 and parameter1 = 4" parameter1=>-4~%	// %-signs are string delimiters here!
  END


  LAUNCH_ACTION_FUNCTION edit_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature	= "aegnoll chevil05 dagnoll gnlcap01 gnleli01 gnlsla01 gnlvet01 gnlwar01 gnoll01 gnollhp1 gnollsu"
    editstring	= ~strengthGT=>13 alignment=>"if gender = summoned then neutral else chaotic_evil"~
  END


  // Mummy race fix

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "fsmumm fsmumm2 fsmumm3 mumgre01 mummy mummy01 riftcr03"
    editstring	= "race=>mummy class=>no_class"
  END


  // Poison Mist can be both summoned and randomly encountered in BG2.
  // so we need both gender=>summoned and genter=>neither versions.

  ACTION_IF NOT FILE_EXISTS_IN_GAME "mistposu.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "mistpo01=>mistposu"
      editstring	= "gender=>summoned"
    END

    LAUNCH_ACTION_FUNCTION edit_creature
      STR_VAR
      creature		= "mistpo01"
      editstring	= "gender=>niether"
    END

    LAUNCH_ACTION_FUNCTION edit_effect
      STR_VAR
      effect		= "sppois"
      editstring	= "resource=>mistposu"
    END
  END


  // Greater Water Elemental class fix

  LAUNCH_ACTION_FUNCTION edit_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature	= "elemhydr elwatg01 ohbwat01"
    editstring	= "class=>elemental_water"
  END


  // Correct BG2 Drizzt's proficiencies and make him dual-wield properly.
  // (using BG1EE's version as the correct one)

  MAKE_PATCH
    remove_items=>"sw1h15 sw1h16 drizzts"
    add_items=>"helmnoan(helmet) sw1h16(weapon1) sw1h15(shield)"
    wipe_proficiencies=>null
    set_proficiencies=>"longsword=>2 scimitarwakisashininjato=>2 dagger=>2 shortbow=>2 2weapon=>3"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "drizzt c6drizz c6drizz2 c6drizz3"
    edits	= "patch_data"
  END
END	// bg2_creature_fixes


