DEFINE_ACTION_FUNCTION tob_spell_fixes
BEGIN
  // Warrior and rogue HLAs are not spells.  They should not have
  // a school or secondary type.  Most don't, but some do, making
  // them incorrectly vulnerable to spells like Breach or Pierce Magic.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spcl904 spcl907 spcl913 spcl914 spcl916 spcl917"
    editstring	= "primary=>0 school_code=>0 secondary=>0"
  END


  // ## FIXED IN EEFP ##
  // Balthasar's "Solar Stance!" is supposed to give him flaming fists,
  // but that part doesn't work as intended due to a missing effect file.

  ACTION_IF NOT FILE_EXISTS_IN_GAME "balth01b.eff"
  BEGIN
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "balth01b"
      editstring	= ~opcode=>146 target=>2 timing=>1 parameter2=>1 resource=>"balth01b"~
    END
  END


  // SPIN200 - Cure Serious Wounds (Imoen innate?)
  //
  // Spells that heal wounds should not cure drunkenness.
  // This makes no real sense whatsoever.
  // NB: Paladin Lay on Hands already doesn't do this.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spin200"
    editstring	= ~delete_effect=>"opcode = 164"~
  END


  // SPIN201 - Neutralize Poison (Imoen innate?)
  //
  // Slow Poison cures drunkeness, but Neutralize Poison does not.  Let's
  // fix this oversight, shall we?

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spin201"
    editstring	= %clone_effect_inline=>~match=>"opcode = 11" opcode=>164~%
  END
END	// tob_spell_fixes


