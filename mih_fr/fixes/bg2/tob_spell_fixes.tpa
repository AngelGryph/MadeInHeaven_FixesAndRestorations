DEFINE_ACTION_FUNCTION tob_spell_fixes
BEGIN
  // Many innates and HLAs are not spells.  They should not have
  // a school or secondary type.  Most don't, but some do, making
  // them incorrectly vulnerable to spells like Breach or Pierce Magic.
  // In addition, many can incorrectly trigger wild surges.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spcl751 spcl801 spcl811 spcl812 spcl813 spcl814 spcl816 spcl817 spcl818 spcl819 spcl820 spcl900 spcl901 spcl902 spcl903 spcl904 spcl905 spcl906 spcl907 spcl908 spcl909 spcl910 spcl911 spcl912 spcl913 spcl914 spcl915 spcl916 spcl917 spcl918 spcl919 spcl920 spcl921 spcl922 spcl924 spcl925 spcl926 spcl927 spcl928 spcl929 spcl930 spcl931 spcl932 spcl933 spcl934"
    editstring	= "primary=>0 school_code=>0 secondary=>0 not_wild_dead=>1 not_wild_surge=>1"
  END


  // ## FIXED IN EEFP ##
  // Balthasar's "Solar Stance!" is supposed to give him flaming fists,
  // but that part doesn't work as intended due to a missing effect file.

  ACTION_IF NOT FILE_EXISTS_IN_GAME "balth01b.eff"
  BEGIN
    LAUNCH_ACTION_FUNCTION make_effect
      STR_VAR
      effect		= "balth01b"
      editstring	= ~opcode=>146 target=>2 timing=>1 parameter2=>1 resource=>"balth01b"~
    END
  END


  // SPIN200 - Cure Serious Wounds (Imoen innate?)
  //
  // Spells that heal wounds should not cure drunkenness.
  // This makes no real sense whatsoever.
  // NB: Paladin Lay on Hands already doesn't do this.

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spin200"
    editstring	= ~delete_effect=>"opcode = 164"~
  END


  // SPIN201 - Neutralize Poison (Imoen innate?)
  //
  // Slow Poison cures drunkenness, but Neutralize Poison does not.  Let's
  // fix this oversight, shall we?

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "spin201"
    editstring	= %clone_effect_inline=>~match=>"opcode = 11" opcode=>164~%
  END
END	// tob_spell_fixes


