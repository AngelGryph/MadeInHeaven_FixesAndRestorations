DEFINE_ACTION_FUNCTION soa_creature_fixes
BEGIN
  // ## FIXED IN EEFP ##
  // Ankheg HD fix

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "ankheg01"
    editstring	= "level1=>8"
  END


  // ## FIXED IN EEFP ##
  // Gnoll strength and alignment fix

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "flind1"
    editstring	= %patch_effect_inline=>~match=>"opcode = 54 and parameter1 = 4" parameter1=>-4~%	// %-signs are string delimiters here!
  END


  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "aegnoll dagnoll gnlcap01 gnleli01 gnlsla01 gnlvet01 gnlwar01 gnoll01 gnollhp1 gnollsu"
    editstring	= ~strengthGT=>13 alignment=>"if gender = summoned then neutral else chaotic_evil"~
  END


  // Prevent racial penalty to hit dwarves/gnomes/halflings
  // from applying to ettins twice.

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "ettin"
    editstring	= ~delete_effect=>"resource is_in [giant1 giant2 giant3]"~
  END


  // Some elementals are not using the right "paws".
  //
  // NB: Correct enchantment level for paws is set in common fixes.

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elairl01"
    editstring	= ~replace_items=>"elairl(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elearl01 elearsu1 mekear01"
    editstring	= ~replace_items=>"elearl(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elearsu kearth mdearth mdearth2 shearth"
    editstring	= ~replace_items=>"elear(weapon1)"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "elefel01 elfirl01"
    editstring	= ~replace_items=>"fireelel(weapon1)"~
  END


  // Some greater elementals have wrong HD number

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "udelda udelde udeldf obsfir06 suelew1 suelew2"
    editstring	= "levelGT=>16"
  END


  // Invisible Stalker can be both summoned and randomly encountered in BG2.
  // so we need both gender=>summoned and gender=>neither versions.

  ACTION_IF NOT FILE_EXISTS_IN_GAME "stalkesu.cre"
  BEGIN
    LAUNCH_ACTION_FUNCTION clone_creature
      STR_VAR
      creature		= "stalke=>stalkesu"
      editstring	= "xp_value=>0 gender=>summoned"
    END

    LAUNCH_ACTION_FUNCTION edit_creature
      STR_VAR
      creature		= "stalke"
      editstring	= "xp_value=>3000 gender=>neither"
    END

    LAUNCH_ACTION_FUNCTION edit_effect
      STR_VAR
      effect		= "spstalk"
      editstring	= "resource=>stalkesu"
    END
  END


  // Minotaur race/class/HD/gender fix
  //
  // Minotaurs have 4 HD, but otherwise have the stats of 8-HD elders.
  // Race is all over the place: gnoll, ogre or umber hulk.

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "icmin01 uhogre03"
    editstring	= "xp_value=>3000 level1GT=>8 race=>minotaur class=>fighter gender=>male"
  END


  // Mummy race fix

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "mumgre01 mummy mummy01 riftcr03"
    editstring	= "race=>mummy class=>no_class"
  END


  // ## FIXED IN EEFP ##
  // Correct BG2 Drizzt's proficiencies and make him dual-wield properly.
  // (using BG1EE's version as the correct one)

  MAKE_PATCH
    remove_items=>"sw1h15 sw1h16 drizzts"
    add_items=>"helmnoan(helmet) sw1h16(weapon1) sw1h15(shield)"
    wipe_proficiencies=>null
    set_proficiencies=>"longsword=>2 scimitarwakisashininjato=>2 dagger=>2 shortbow=>2 2weapon=>3"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "drizzt c6drizz c6drizz2 c6drizz3"
    edits	= "patch_data"
  END


  // Shambling Mound almost-everything fix

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "smound01 smoundsu"
    editstring	= ~general=>plant race=>shambling_mound class=>no_class gender=>"if gender = female then neither else no_change" sex=>4~
  END


  // Skeleton Warrior race and immunity fixes

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "ar18skel ceskel01 endske01 endske02 endske03 endske04 firmon02 grskel1 grskel2 grtomb01 hgskl04 nevm2 riftcr02 rskel02 sahskel skelwa skelwa01 skelwa02 skelwa03 skelwasu suundead tanskw1"
    editstring	= ~race=>skeleton resist_slashing=>50 add_effect_inline=>"opcode=>297 target=>1 timing=>1 parameter2=>1"~
  END


  // Mind flayer resistances correction
  //
  // Some mind flayers are immune to all elements, but it's not consistently
  // applied to all of them.  There is no reason for this, and they have enough
  // magic resistance already without it.

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature    = "mindfl01"
    editstring  = ~resist_fire=>0 resist_cold=>0 resist_electricity=>0 resist_acid=>0 resist_magic_fire=>0 resist_magic_cold=>0~
  END


  // Ulitharid statistics corrections
  //
  // Ulitharids should have 11 hd, thac0 9, intelligence 20
  // and be worth 11,000 xp.  All instances in the game got
  // at least one of them wrong.
  //
  // Ulitharid are also using the generic mind flayer psionic
  // blast instead of their own stronger one.

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature    = "mindul01 ppmind01"
    editstring  = ~level1=>11 xp_value=>11000 thac0=>9 intelligence=>20~
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "%MIND_FLAYER_PSIONIC_BLAST%=>%ULITHARID_PSIONIC_BLAST%"
    editstring	= ~patch_effect_inline=>"save_bonus=>-4 duration_if_variable=>120~
  END

  LAUNCH_ACTION_FUNCTION ALTER_SCRIPT_BLOCK
    STR_VAR
    script	= "flayer02"
    swap_out	= "MIND_FLAYER_PSIONIC_BLAST"
    swap_in	= "ULITHARID_PSIONIC_BLAST"
  END
END	// creature_fixes


