DEFINE_ACTION_FUNCTION small_box
BEGIN
  // MISC03 - Small Box
  //
  // An unused miscelaneous item.  No clues as to what its purpose
  // may have been.  Let's just make it a handly little container
  // for the early game.

  itm.edit[misc03]
  [
    m_price = 10
    m_category = 0x24
    m_critical_item = 1
    m_unidentified_description := @0
  ]

  sto.make[misc03]
  [
    m_name-string = 6962
    m_type = 5
    m_can_buy = 1
    m_can_sell = 1
    m_capacity = 20
    PATCH_FOR_EACH foo IN 0x00 0x01 0x03 0x04 0x05 0x06 0x07 0x08 0x09 0x0a 0x0b 0x0d 0x0e 0x10 0x12 0x18 0x1f 0x21 0x22 0x23
    BEGIN
      m.itempurchased.add{s_category = %foo%}
    END
  ]

  LAF install STR_VAR file = "imisc03.bam" location = "resources" END

  ACTION_IF is_eet
  BEGIN
    itm.copy[misc03=>misc03%eet_var%]
    sto.copy[misc03=>misc03%eet_var%]
  END

  ACTION_IF is_bg1
  BEGIN
    sto.edit[friend]
    [
      m.item.add{s_resref := "misc03%eet_var%";;s_number_in_stock = 1}
    ]
  END

  ACTION_IF is_bg2
  BEGIN
    sto.edit[shop01]
    [
      m.item.add{s_resref := "misc03";;s_number_in_stock = 1}
    ]
  END
END	// small_box
