DEFINE_ACTION_FUNCTION scroll_restorations
BEGIN
  // Restore dummied out priest spell scrolls in BG1 and BG2

  ACTION_CLEAR_ARRAY spell_scrolls
  ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_scrolls
  BEGIN
    scrl19 =>	CLERIC_BLESS
    scrl20 =>	CLERIC_COMMAND
    scrl21 =>	CLERIC_CURE_LIGHT_WOUNDS
    scrl22 =>	CLERIC_DETECT_EVIL
    scrl23 =>	CLERIC_ENTANGLE
    scrl24 =>	CLERIC_MAGIC_STONE
    scrl25 =>	CLERIC_PROTECT_FROM_EVIL
    scrl26 =>	CLERIC_REMOVE_FEAR
    scrl27 =>	CLERIC_SANCTUARY
    scrl28 =>	CLERIC_SHILLELAGH
    scrl29 =>	CLERIC_AID
    scrl30 =>	CLERIC_BARKSKIN
    scrl31 =>	CLERIC_CHANT
    scrl32 =>	CLERIC_CHARM_PERSON
    scrl33 =>	CLERIC_FIND_TRAPS
    scrl34 =>	CLERIC_FLAME_BLADE
    scrl35 =>	CLERIC_GOOD_BERRIES
    scrl36 =>	CLERIC_HOLD_PERSON
    scrl37 =>	CLERIC_KNOW_ALIGNMENT
    scrl38 =>	CLERIC_RESIST_FIRE
    scrl39 =>	CLERIC_SILENCE_15_FOOT
    scrl40 =>	CLERIC_SLOW_POISON
    scrl41 =>	CLERIC_SPIRITUAL_HAMMER
    scrl42 =>	CLERIC_DRAW_UPON_HOLY_MIGHT
    // scrl43 is dummied out CLERIC_RESIST_COLD (reused for Cure Blind/Deaf)
    scrl44 =>	CLERIC_ANIMATE_DEAD
    scrl45 =>	CLERIC_CALL_LIGHTNING
    scrl46 =>	CLERIC_DISPEL_MAGIC
    scrl47 =>	CLERIC_GLYPH_OF_WARDING
    scrl48 =>	CLERIC_HOLD_ANIMAL
    scrl49 =>	CLERIC_PROTECTION_FROM_FIRE
    scrl50 =>	CLERIC_REMOVE_CURSE
    scrl51 =>	CLERIC_REMOVE_PARALYSIS
    scrl52 =>	CLERIC_INVISIBILITY_PURGE
    scrl53 =>	CLERIC_MISCAST_MAGIC
    scrl54 =>	CLERIC_RIGID_THINKING
    scrl55 =>	CLERIC_STRENGTH_OF_ONE
    scrl57 =>	CLERIC_ANIMAL_SUMMONING_1
    scrl60 =>	CLERIC_ANIMAL_SUMMONING_2
  END

  ACTION_PHP_EACH spell_scrolls AS scrollname => idsname
  BEGIN
    LAUNCH_ACTION_FUNCTION make_scroll_of_spell
      STR_VAR
      resref		= EVALUATE_BUFFER "%%idsname%%"
      scrollname
      scrollicon	= "i%scrollname%"
    END
  END


  // Replace some changed/missing icons with their spell icon equivalents.

  LAUNCH_ACTION_FUNCTION clone_template
    STR_VAR
    file_list	= "sppr101a=>iscrl19 sppr202a=>iscrl30 sppr402a=>iscrl57 sppr501a=>iscrl60"
    file_ext	= "bam"
  END


  // Add scrolls to selected temples

  ACTION_CLEAR_ARRAY cleric_scrolls
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cleric_scrolls
  BEGIN
    litvar_scroll_list_1=>"[scrl19 scrl20 scrl21 scrl22 scrl25 scrl26 scrl27]"
    litvar_scroll_list_2=>"[scrl29 scrl31 scrl33 scrl36 scrl37 scrl38 scrl39 scrl40 scrl41 scrl42]"
    litvar_scroll_list_3=>"[scrl46 scrl47 scrl49 scrl50 scrl51 scrl52 scrl53 scrl54 scrl55]"
    litvar_scroll_list_4=>"[scrl56 scrl58 scrl59 scrl61 scrl62 scrl63]"
    delete_item'1=>"item_resource is_in scroll_list_1"
    delete_item'2=>"item_resource is_in scroll_list_2"
    delete_item'3=>"item_resource is_in scroll_list_3"
    delete_item'4=>"item_resource is_in scroll_list_4"
    add_item_inline'1=>~number_to_add=>"length scroll_list_1" at_end=>1 item_resource=>"entry_index from scroll_list_1" item_charges_1=>1 item_num_in_stock=>5~
    add_item_inline'2=>~number_to_add=>"length scroll_list_2" at_end=>1 item_resource=>"entry_index from scroll_list_2" item_charges_1=>1 item_num_in_stock=>3~
    add_item_inline'3=>~number_to_add=>"length scroll_list_3" at_end=>1 item_resource=>"entry_index from scroll_list_3" item_charges_1=>1 item_num_in_stock=>2~
    add_item_inline'4=>~number_to_add=>"length scroll_list_4" at_end=>1 item_resource=>"entry_index from scroll_list_4" item_charges_1=>1 item_num_in_stock=>1~
  END

  
  ACTION_CLEAR_ARRAY druid_scrolls
  ACTION_DEFINE_ASSOCIATIVE_ARRAY druid_scrolls
  BEGIN
    litvar_scroll_list_1=>"[scrl19 scrl21 scrl23 scrl24 scrl27 scrl28]"
    litvar_scroll_list_2=>"[scrl32 scrl33 scrl34 scrl35 scrl37 scrl40]"
    litvar_scroll_list_3=>"[scrl45 scrl46 scrl48 scrl49 scrl52 scrl53 scrl54 scrl55]"
    litvar_scroll_list_4=>"[scrl56 scrl57 scrl59 scrl60 scrl61 scrl62]"
    delete_item'1=>"item_resource is_in scroll_list_1"
    delete_item'2=>"item_resource is_in scroll_list_2"
    delete_item'3=>"item_resource is_in scroll_list_3"
    delete_item'4=>"item_resource is_in scroll_list_4"
    add_item_inline'1=>~number_to_add=>"length scroll_list_1" at_end=>1 item_resource=>"entry_index from scroll_list_1" item_charges_1=>1 item_num_in_stock=>5~
    add_item_inline'2=>~number_to_add=>"length scroll_list_2" at_end=>1 item_resource=>"entry_index from scroll_list_2" item_charges_1=>1 item_num_in_stock=>3~
    add_item_inline'3=>~number_to_add=>"length scroll_list_3" at_end=>1 item_resource=>"entry_index from scroll_list_3" item_charges_1=>1 item_num_in_stock=>2~
    add_item_inline'4=>~number_to_add=>"length scroll_list_4" at_end=>1 item_resource=>"entry_index from scroll_list_4" item_charges_1=>1 item_num_in_stock=>1~
  END
  
  ACTION_CLEAR_ARRAY black_market
  ACTION_DEFINE_ASSOCIATIVE_ARRAY black_market
  BEGIN
    add_item_inline=>~at_end=>1 item_resource=>"scrl44" item_charges_1=>1 item_num_in_stock=>2~
  END
  
  ACTION_IF is_bg1
  BEGIN
    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "tem0002 tem0131 tem0132 tem2601 tem3402 tem4003 tem4802"
      edits	= "cleric_scrolls"
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "stoblack"
      edits	= "black_market"
    END
  END

  ACTION_IF has_sod
  BEGIN
    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "bdbeleg3 bddosia bdff1000 bdpolvi"
      edits	= "cleric_scrolls"
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "bdonoro"
      edits	= "druid_scrolls"
    END
  END

  ACTION_IF is_bg2
  BEGIN
    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "govwau01 kpchap01 ppumb01 temhelm temlath temtalos temsup"
      edits	= "cleric_scrolls"
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "murcrag sahpr1 suelf10 trrak01"
      edits	= "druid_scrolls"
    END

    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "arled bmthief roger shthstor"
      edits	= "black_market"
    END
  END

  ACTION_IF has_tob
  BEGIN
    LAUNCH_ACTION_FUNCTION edit_store
      STR_VAR
      store	= "garlena amcler02 sartem01"
      edits	= "cleric_scrolls"
    END
  END
END	// expanded_temple_services


