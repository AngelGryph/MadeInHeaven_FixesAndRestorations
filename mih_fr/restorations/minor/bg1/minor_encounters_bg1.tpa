DEFINE_ACTION_FUNCTION minor_encounters_bg1
BEGIN
  // Restore doppleganger Erik

  cre.edit[erik3]
  [
    m.cre_insert_script{initdlg|loc="high"}
    m_dialog := "erik3"
    m_class := "fighter_thief"
    m_ac = 10
    m_str = 14
    m_dex = 16
    m.cre_enforce_thac0{at_best}
    m.cre_enforce_saves{at_best}
    m.cre_add_items{bow04(weapon1) arow02(quiver1,20) sw1h08(weapon2) leat05(armor)|replace:i=1}
    m.cre_set_proficiencies{bow=>3 short_sword=>3}
  ]

  are.edit[%Candlekeep_Catacombs_L2%]
  [
    m.actor.add{s_name:="Doppleganger Erik" s_resref:="erik3" s_x=1430 s_y=260 s_face:="SE"}
  ]


  // Restore statue Corianne

  cre.copy[corian|source_location="resources"]

  are.edit[%MutaminsGarden%]
  [
    m.actor.add{s_name:="Corianna" s_resref:="corian" s_x=1210 s_y=2580 s_face:="E"}
  ]

<<<<<<<< .../stratagems-inline/corianne_fixes.d
REPLACE_STATE_TRIGGER "corian" 0
  ~NumTimesTalkedTo(0) ReactionLT(LastTalkedToBy(Myself), FRIENDLY_LOWER)~

ADD_TRANS_ACTION "corian"
BEGIN 0 END	// State(s)
BEGIN 0 END	// Transaction(s)
  ~AddexperienceParty(300) EscapeArea()~

REPLACE_STATE_TRIGGER "corian" 1
  ~NumTimesTalkedTo(0)~

ADD_TRANS_ACTION "corian"
BEGIN 1 END	// State(s)
BEGIN 0 END	// Transaction(s)
  ~AddexperienceParty(300) GiveItem("potn38", LastTalkedToBy(Myself)) EscapeArea()~
>>>>>>>>

  LAF install INT_VAR inline = 1 STR_VAR file = "corianne_fixes.d" END
END	// minor_encounters_bg1


