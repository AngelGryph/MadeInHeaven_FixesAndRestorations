DEFINE_ACTION_FUNCTION quoningar
BEGIN
  cre.copy[quonin|source_location="resources"]

  are.edit[%Beregost_JovialJuggler_L2F%]
  [
    m.actor.add{s_name:="Quoningar" s_resref:="quonin" s_x=125 s_y=355 s_face:="NNW"}
  ]

<<<<<<<< .../stratagems-inline/quoningar_fixes.d
REPLACE_STATE_TRIGGER "quonin" 0
  ~ReactionLT(LastTalkedToBy(Myself), NEUTRAL_LOWER)~

REPLACE_STATE_TRIGGER "quonin" 1
  ~Global("HelpQuoningar", "GLOBAL", 0)~

ADD_TRANS_ACTION "quonin"
BEGIN 1 END	// State(s)
BEGIN 0 END	// Transaction(s)
  ~SetGlobal("HelpQuoningar", "GLOBAL", 1)~

ALTER_TRANS "quonin"
BEGIN 1 END	// State(s)
BEGIN 0 END	// Transaction(s)
BEGIN
  "UNSOLVED_JOURNAL" ~@2~
END

REPLACE_STATE_TRIGGER "quonin" 2
  ~Global("HelpQuoningar", "GLOBAL", 1) !PartyHasItem("potn37")~

REPLACE_STATE_TRIGGER "quonin" 3
  ~Global("HelpQuoningar", "GLOBAL", 1) PartyHasItem("potn37") !Dead("bassilus")~

ADD_TRANS_ACTION "quonin"
BEGIN 3 END	// State(s)
BEGIN 0 END	// Transaction(s)
  ~EraseJournalEntry(@2) SetGlobal("HelpQuoningar", "GLOBAL", 2) TakePartyItemNum("potn37", 1) GiveItem("hamm02", LastTalkedToBy(Myself))~

ALTER_TRANS "quonin"
BEGIN 3 END	// State(s)
BEGIN 0 END	// Transaction(s)
BEGIN
  "SOLVED_JOURNAL" ~@3~
END

REPLACE_STATE_TRIGGER "quonin" 4
  ~Global("HelpQuoningar", "GLOBAL", 1) PartyHasItem("potn37") Dead("bassilus")~

ADD_TRANS_ACTION "quonin"
BEGIN 4 END	// State(s)
BEGIN 0 END	// Transaction(s)
  ~EraseJournalEntry(@2) SetGlobal("HelpQuoningar", "GLOBAL", 2) TakePartyItemNum("potn37", 1) GiveItem("potn33", LastTalkedToBy(Myself))~

ALTER_TRANS "quonin"
BEGIN 4 END	// State(s)
BEGIN 0 END	// Transaction(s)
BEGIN
  "SOLVED_JOURNAL" ~@4~
END

REPLACE_STATE_TRIGGER "quonin" 5
  ~True()~
>>>>>>>>

  LAF install INT_VAR inline = 1 STR_VAR file = "quoningar_fixes.d" END

  ADD_JOURNAL @2 @3 @4
END	// quoningar


