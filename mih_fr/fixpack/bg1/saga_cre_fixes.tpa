DEFINE_ACTION_FUNCTION saga_cre_fixes
BEGIN
  // Dread Wolves are undead and should use a proper paw.
  cre.edit[wolfd1 wolfdr]
  [
    m_general := "undead"
    m.cre_add_items{wolfdr1(weapon1) wolfdr1(weapon2)|replace:i=1}
  ]


  // Winter Wolves are not using their dedicated script.
  cre.edit[wolfwi wolfwisu wolfwwsu]
  [
    m_script_default := "wntrwolf"
  ]


  // Kryla is a wolfwere, not a werewolf.

  cre.edit[kryla]
  [
    m_class := "wolfwere"
  ]


  // Riggilo was probably badly copied from Pratt, wields a weapon
  // he has no proficiency in and has an item he should not have.

  cre.edit[riggilo]
  [
    m.cre_remove_items{scrl2n}
    m.cre_set_proficiencies{short_sword=>4}
  ]


  // Wraith Spiders are undead and should be immune to cold.

  cre.edit[spidwr]
  [
    m_general := "undead"
    m_resist_cold = 100
    m_resist_magic_cold = 100
  ]


  // Some xvarts have higher xp values but aren't
  // actually any tougher than others.

  cre.edit[xvart02 xvart03 xvart2 xvarth]
  [
    m_level1 = 2
    m_hp = 15
    m_thac0 = 18
  ]
END	// saga_cre_fixes


