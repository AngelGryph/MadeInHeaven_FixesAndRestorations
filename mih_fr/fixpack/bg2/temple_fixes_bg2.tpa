DEFINE_ACTION_FUNCTION temple_fixes_bg2
BEGIN
  // Two temples in BP sell cures that are not in speldesc.2da.
  // Let's add these descriptions and also give these spells to
  // the other temples.
  //
  // NB: Further standardizing and streamlining of available
  //     cures is in my MiH Spellpack mod.

  // Cure Disease
  OUTER_SET strref = RESOLVE_STR_REF (@0)
  APPEND "speldesc.2da" "SPPR317     %strref%" UNLESS "SPPR317"

  // Neutralize Poison
  OUTER_SET strref = RESOLVE_STR_REF (@1)
  APPEND "speldesc.2da" "SPPR404     %strref%" UNLESS "SPPR404"

  // Lesser Restoration
  OUTER_SET strref = RESOLVE_STR_REF (@2)
  APPEND "speldesc.2da" "SPPR417     %strref%" UNLESS "SPPR417"


  sto.edit[amcler02 bhelm dawnmas doghma garlena govwau01 helmpr kpchap01 ohdhsen sahpr1 sartem01 slilmat suelf10 talmiss temhelm temlath temple temsup temtalos wilmat]
  [
    // Cure Medium Wounds
    m.cure.clone{s_price = 100;;s_resref := "sppr315"|match=~"%s_resref%" == "sppr307"~}

    // Cure Disease
    m.cure.clone{s_price = 150;;s_resref := "sppr317"|match=~"%s_resref%" == "sppr315"~}

    // Neutralize Poison (replaces Slow Poison)
    m.cure.alter{s_price = 300;;s_resref := "sppr404"|match=~"%s_resref%" == "sppr212"~}

    // Slow Poison (reinsert after Cure Light)
    m.cure.clone{s_price = 100;;s_resref := "sppr212"|match=~"%s_resref%" == "sppr103"~}

    // Lesser Restoration
    m.cure.clone{s_price = 350;;s_resref := "sppr417"|match=~"%s_resref%" == "sppr404"~}
  ]
END	// temple_fixes_bg2


