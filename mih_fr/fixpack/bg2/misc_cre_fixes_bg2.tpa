DEFINE_ACTION_FUNCTION misc_cre_fixes_bg2
BEGIN
  // Dread Wolves are undead and should use a proper paw.
  cre.edit[wolfdr01]
  [
    m_general := "undead"
    m.cre_add_items{wolfdr1(weapon1) wolfdr1(weapon2)|replace:i=1}
  ]


  // Winter Wolves are not using their dedicated script.
  cre.edit[obsice03 wolfwi01 wolfwisu wolfwwsu]
  [
    m_script_default := "wntrwolf"
  ]


  // Mark mummies as actually, y'know, mummies.

  cre.edit[fsmumm fsmumm2 fsmumm3 mumgre01 mummy mummy01 riftcr03]
  [
    m_race := "mummy"
    m_class := "no_class"
  ]


  // Likewise, let's call green dragons actually green dragons.
  
  cre.edit[bazdra03 fsdragon]
  [
    class := "green_dragon"
  ]


  // Wraith Spiders are undead and should be immune to cold.
  //
  // NB: Leave ttspid alone, it's for the tutorial.

  cre.edit[spidwr01]
  [
    m_general := "undead"
    m_resist_cold = 100
    m_resist_magic_cold = 100
  ]


  // Vortex Spiders have incorrect number of HD, should be 7.
  // The rest of their stats do match.

  cre.edit[ohbmena7 smspid02 spidvo01]
  [
    m_level1 = 7
  ]
END	// misc_cre_fixes_bg2


