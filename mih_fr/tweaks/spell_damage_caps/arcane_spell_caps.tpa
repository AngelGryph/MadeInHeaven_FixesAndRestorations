DEFINE_ACTION_FUNCTION arcane_spell_caps
BEGIN
  // 1-3 - caps at 10
  // 4-5 - caps at 15
  // 6-7 - caps at 20
  // 8-9 - caps at 25
 

  // SPWI313 - Skull Trap
  //
  // As a third level spell, this should cap at level 10.
  // NB: Damage correction is already done in the fixpack.

  spl.edit[spwi313]
  [
    m.ab.delete{s_level > 10}
  ]


  // SPWI503 - Cone of Cold
  //
  // As a fifth level spell, this should cap at level 15.

  spl.edit[spwi503]
  [
    m.ab.delete{s_level > 15}
  ]


  // spwi615 - Chain Lightning (wizard)
  // spdr601 - Chain Lightning (avenger)
  //
  // This should deal 1d6 per level, not 1d6 per two levels.

  spl.edit[spwi615 spdr601]
  [
    m.ab.delete{s_level > 1}
    m.ab.clone{s_level = entry_index + 13|number:i=8}
    m.ab_fx.alter{s_dicenumber = p_level > 1 ? p_level : 12|match="s_opcode = 12"}
  ]


  // spwi712 - Delayed Blast Fireball
  //
  // As a 7th level spell, this should scale up to level 20.

  spl.edit[spwi712]
  [
    m.ab.delete{s_level > 1}
    m.ab.clone{s_level = entry_index + 15|number:i=6}
    m.ab_fx.alter{s_parameter1 = p_level > 1 ? p_level : 14;;s_dicenumber = p_level > 1 ? p_level : 14|match="s_opcode = 12"}
  ]


  // spwi812 - Abi-Dalzim's Horrid Wilting
  //
  // As an 8th level spell, this should scale up to level 25.

  spl.edit[spwi812]
  [
    m.ab.delete{s_level > 1}
    m.ab.clone{s_level = entry_index + 17|number:i=9}
    m.ab_fx.alter{s_dicenumber = p_level > 1 ? p_level : 16|match="s_opcode = 12"}
  ]
END	// arcane_spell_caps


